Функция ОстаткиНКД(Параметры)

	СчетаУчетаОблигаций = УчетЕПСПовтИсп.СчетаУчетаОблигаций();

	#Область БЛОКИРОВКА
	Блокировка = Новый БлокировкаДанных;
	Для Каждого СчетБлокировки Из СчетаУчетаОблигаций Цикл
		БлокировкаЕПС = Блокировка.Добавить("РегистрБухгалтерии.ЕПС");
		БлокировкаЕПС.УстановитьЗначение("Период",    Новый Диапазон(, Параметры.Дата));
		БлокировкаЕПС.УстановитьЗначение("Портфель",  Параметры.Портфель);
		БлокировкаЕПС.УстановитьЗначение("Счет",  СчетБлокировки);
		БлокировкаЕПС.ИсточникДанных = Параметры.ТаблицаАктивов;
		БлокировкаЕПС.ИспользоватьИзИсточникаДанных("Субконто1", "Актив");
	КонецЦикла;
	Блокировка.Заблокировать();
	#КонецОбласти

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕПСОстатки.Счет КАК Счет,
		|	ЕПСОстатки.Субконто1 КАК Актив,
		|	ЕПСОстатки.Субконто3 КАК Партия,
		|	ЕПСОстатки.КоличествоОстатокДт КАК КоличествоОстатокЦБ,
		|	ЕПСОстатки.Валюта КАК ВалютаЦБ,
		|	ЕСТЬNULL(Облигации.ДатаПогашения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПогашения
		|ПОМЕСТИТЬ ОстаткиЦБ
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(
		|			&МоментВремени,
		|			Счет В (&Счет),
		|			,
		|			Портфель = &Портфель
		|				И Субконто1 В (&АктивыОблигации)) КАК ЕПСОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Облигации КАК Облигации
		|		ПО (ВЫРАЗИТЬ(ЕПСОстатки.Субконто1 КАК Справочник.Активы).Объект = Облигации.Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НКДОстатки.Счет КАК Счет,
		|	НКДОстатки.Субконто3 КАК ПартияНКД,
		|	НКДОстатки.Субконто1 КАК АктивНКД,
		|	НКДОстатки.Валюта КАК ВалютаЦБ,
		|	НКДОстатки.СуммаОстатокДт КАК ОстатокНКДРуб,
		|	НКДОстатки.ВалютнаяСуммаОстатокДт КАК ОстатокНКДВал,
		|	НКДОстатки.СуммаНУОстатокДт КАК ОстатокНКДНу,
		|	0 КАК ОстатокУКДРуб,
		|	0 КАК ОстатокУКДВал,
		|	0 КАК ОстатокУКДНу
		|ПОМЕСТИТЬ НКДОстатки
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(
		|			&МоментВремени,
		|			Счет В (&Счет),
		|			,
		|			Портфель = &Портфель
		|				И Субконто1 В (&АктивыОблигации)
		|				И Субконто2 = ЗНАЧЕНИЕ(Перечисление.ЧастиЦенныхБумаг.КупонныйДоход)) КАК НКДОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УКДОстатки.Счет,
		|	УКДОстатки.Субконто3,
		|	УКДОстатки.Субконто1,
		|	УКДОстатки.Валюта,
		|	0,
		|	0,
		|	0,
		|	УКДОстатки.СуммаОстатокДт,
		|	УКДОстатки.ВалютнаяСуммаОстатокДт,
		|	УКДОстатки.СуммаНУОстатокДт
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(
		|			&МоментВремени,
		|			Счет В (&Счет),
		|			,
		|			Портфель = &Портфель
		|				И Субконто1 В (&АктивыОблигации)
		|				И Субконто2 = ЗНАЧЕНИЕ(Перечисление.ЧастиЦенныхБумаг.УплаченныйКупонныйДоход)) КАК УКДОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НКДОстатки.Счет КАК Счет,
		|	НКДОстатки.ПартияНКД КАК ПартияНКД,
		|	НКДОстатки.АктивНКД КАК АктивНКД,
		|	НКДОстатки.ВалютаЦБ КАК ВалютаЦБ,
		|	СУММА(НКДОстатки.ОстатокНКДРуб) КАК ОстатокНКДРуб,
		|	СУММА(НКДОстатки.ОстатокНКДВал) КАК ОстатокНКДВал,
		|	СУММА(НКДОстатки.ОстатокНКДНу) КАК ОстатокНКДНу,
		|	СУММА(НКДОстатки.ОстатокУКДРуб) КАК ОстатокУКДРуб,
		|	СУММА(НКДОстатки.ОстатокУКДВал) КАК ОстатокУКДВал,
		|	СУММА(НКДОстатки.ОстатокУКДНу) КАК ОстатокУКДНу
		|ПОМЕСТИТЬ НКД_ОстаткиСвернутые
		|ИЗ
		|	НКДОстатки КАК НКДОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	НКДОстатки.ВалютаЦБ,
		|	НКДОстатки.ПартияНКД,
		|	НКДОстатки.Счет,
		|	НКДОстатки.АктивНКД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(КупонныеПериоды.ДатаОкончанияКупонногоПериода) КАК ДатаОкончанияКупонногоПериода,
		|	Активы.Ссылка КАК Актив
		|ПОМЕСТИТЬ ВТ_ДатыОкончанияПоследнегоКупона
		|ИЗ
		|	Справочник.Активы КАК Активы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КупонныеПериоды КАК КупонныеПериоды
		|		ПО Активы.Объект = КупонныеПериоды.Владелец
		|ГДЕ
		|	Активы.Ссылка В(&АктивыОблигации)
		|	И НЕ КупонныеПериоды.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	Активы.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиЦБ.Счет КАК СчетУчетаАктива,
		|	ОстаткиЦБ.Партия КАК Партия,
		|	ОстаткиЦБ.Актив КАК Актив,
		|	ОстаткиЦБ.ВалютаЦБ КАК ВалютаЦБ,
		|	ЕСТЬNULL(НКД_ОстаткиСвернутые.ОстатокНКДРуб, 0) КАК ОстатокНКДРуб,
		|	ЕСТЬNULL(НКД_ОстаткиСвернутые.ОстатокНКДВал, 0) КАК ОстатокНКДВал,
		|	ЕСТЬNULL(НКД_ОстаткиСвернутые.ОстатокНКДНу, 0) КАК ОстатокНКДНу,
		|	ЕСТЬNULL(НКД_ОстаткиСвернутые.ОстатокУКДРуб, 0) КАК ОстатокУКДРуб,
		|	ЕСТЬNULL(НКД_ОстаткиСвернутые.ОстатокУКДВал, 0) КАК ОстатокУКДВал,
		|	ЕСТЬNULL(НКД_ОстаткиСвернутые.ОстатокУКДНу, 0) КАК ОстатокУКДНу,
		|	ОстаткиЦБ.КоличествоОстатокЦБ КАК КоличествоОстатокЦБ,
		|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(ВТ_ДатыОкончанияПоследнегоКупона.ДатаОкончанияКупонногоПериода, ДАТАВРЕМЯ(1, 1, 1)), ДЕНЬ) = НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ)
		|		И НАЧАЛОПЕРИОДА(ЕСТЬNULL(ВТ_ДатыОкончанияПоследнегоКупона.ДатаОкончанияКупонногоПериода, ДАТАВРЕМЯ(1, 1, 1)), ДЕНЬ) = НАЧАЛОПЕРИОДА(ОстаткиЦБ.ДатаПогашения, ДЕНЬ) КАК ЭтоПоследнийКупон,
		|	ЛОЖЬ КАК ПогашениеПоОферте,
		|	ОстаткиЦБ.Актив.Объект.Эмитент.НеРезидент КАК ЭмитентНеРезидент
		|ИЗ
		|	ОстаткиЦБ КАК ОстаткиЦБ
		|		ЛЕВОЕ СОЕДИНЕНИЕ НКД_ОстаткиСвернутые КАК НКД_ОстаткиСвернутые
		|		ПО (НКД_ОстаткиСвернутые.Счет = ОстаткиЦБ.Счет)
		|			И (НКД_ОстаткиСвернутые.АктивНКД = ОстаткиЦБ.Актив)
		|			И (НКД_ОстаткиСвернутые.ПартияНКД = ОстаткиЦБ.Партия)
		|			И (НКД_ОстаткиСвернутые.ВалютаЦБ = ОстаткиЦБ.ВалютаЦБ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыОкончанияПоследнегоКупона КАК ВТ_ДатыОкончанияПоследнегоКупона
		|		ПО ОстаткиЦБ.Актив = ВТ_ДатыОкончанияПоследнегоКупона.Актив";

	Запрос.УстановитьПараметр("Счет", СчетаУчетаОблигаций);
	Запрос.УстановитьПараметр("Портфель", Параметры.Портфель);
	Запрос.УстановитьПараметр("ДатаДокумента", Параметры.Дата);
	Запрос.УстановитьПараметр("АктивыОблигации", УчетЕПС.ОблигацииИз(Параметры.ТаблицаАктивов));

	Если ТипЗнч(Параметры) = Тип("Структура") и Параметры.Свойство("МоментВремени") и ЗначениеЗаполнено(Параметры.МоментВремени) Тогда
		Запрос.УстановитьПараметр("МоментВремени", Параметры.МоментВремени);
	Иначе
		Запрос.УстановитьПараметр("МоментВремени", Параметры.Регистратор.МоментВремени());
	КонецЕсли;


	ОбщегоНазначенияДУ.ДобавитьВЛогОтладки(Запрос, "ОбщийМодуль.УчетАктивовЕПС.ОстаткиНКД");
	ОстаткиНКД = Запрос.Выполнить().Выгрузить();

	Для Каждого СтрокаОстатков Из ОстаткиНКД Цикл
		НайденныйАктив = Параметры.ТаблицаАктивов.Найти(СтрокаОстатков.Актив, "Актив");
		Если НайденныйАктив <> Неопределено
			И Параметры.ТаблицаАктивов.Колонки.Найти("ПогашениеПоОферте") <> Неопределено Тогда
			СтрокаОстатков.ПогашениеПоОферте = НайденныйАктив.ПогашениеПоОферте;
			Если СтрокаОстатков.ПогашениеПоОферте Тогда
				СтрокаОстатков.ЭтоПоследнийКупон = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат ОстаткиНКД;

КонецФункции