Процедура ПолучитьОстаткиПоЕПС(СписокАктивовПоОстаткам)
	
	Если НЕ ЕПС Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЕПСОстатки.Субконто1 КАК Актив
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(&Период, Счет В (&МассивСчетовОблигаций), , Портфель = &Портфель) КАК ЕПСОстатки
		|ГДЕ
		|	(&НУ ИЛИ &ЕПС)";
	
	МетодНачисленияАмортизацииПремииДисконта = МетодНачисленияАмортизацииПремииДисконта();
	Если МетодНачисленияАмортизацииПремииДисконта = Перечисления.МетодыНачисленияАмортизацииПремииДисконта.Линейный
		ИЛИ МетодНачисленияАмортизацииПремииДисконта = Перечисления.МетодыНачисленияАмортизацииПремииДисконта.ПоЭСП
		ИЛИ МетодНачисленияАмортизацииПремииДисконта = Перечисления.МетодыНачисленияАмортизацииПремииДисконта.ЛинейноИнтервальный Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НУ", "ИСТИНА");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕПС", "ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕПС", "ЕПСОстатки.СуммаОстатокДт > 0");
		Если НУ_ЕПС Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НУ", "ЕПСОстатки.СуммаНУОстатокДт > 0");
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	МассивСчетовОблигаций = УчетЕПСПовтИсп.СчетаУчетаОблигаций();
	
	Запрос.УстановитьПараметр("Портфель", Портфель);
	Запрос.УстановитьПараметр("Период", НачалоДня(Дата));
	Запрос.УстановитьПараметр("НУ", НУ_ЕПС);
	Запрос.УстановитьПараметр("МассивСчетовОблигаций", МассивСчетовОблигаций);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СписокАктивовПоОстаткам.Добавить(Выборка.Актив);
	КонецЦикла;
	
КонецПроцедуры