Функция ОстаткиАктивовПоСделкамРЕПО()
	
	Запрос = Новый Запрос;
	
	МассивСубконто = Новый Массив;
	МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоЕПС.Сделка);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЕПСОстатки.Субконто1 КАК СделкаРЕПО
		|ПОМЕСТИТЬ ВТ_СделкиРЕПО
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(&Дата, Счет В (&Счет47010), &МассивСубконто, Портфель = &Портфель) КАК ЕПСОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СделкаРЕПО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СделкиРЕПО.Ссылка КАК СделкаРЕПО,
		|	СделкиРЕПО.Актив КАК Актив,
		|	СделкиРЕПО.Актив.Объект.Эмитент КАК Эмитент,
		|	СделкиРЕПО.ВалютаДокумента КАК Валюта,
		|	СделкиРЕПО.ВидОбращаемости КАК ВидОбращаемости,
		|	СделкиРЕПО.Количество КАК Количество,
		|	СделкиРЕПО.Контрагент КАК Контрагент,
		|	СделкиРЕПО.КратностьВзаиморасчетов КАК Кратность,
		|	СделкиРЕПО.КурсВзаиморасчетов КАК Курс
		|ИЗ
		|	ВТ_СделкиРЕПО КАК ВТ_СделкиРЕПО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.СделкиРЕПО КАК СделкиРЕПО
		|		ПО ВТ_СделкиРЕПО.СделкаРЕПО = СделкиРЕПО.Ссылка
		|			И СделкиРЕПО.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСделкаРЕПО.ОбратныйРЕПО)
		|			И НЕ СделкиРЕПО.СделкаНапрямуюСБиржей
		|			И СделкиРЕПО.Актив.Объект ССЫЛКА Справочник.Облигации
		|			И ВЫБОР
		|				КОГДА &ПоВыбраннымАктивам
		|					ТОГДА СделкиРЕПО.Актив В (&СписокВыбранныхАктивов)
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ";
	
	Запрос.Текст = ТекстЗапроса;
	
	МассивСчетовОблигаций = УчетЕПСПовтИсп.СчетаУчетаОблигаций();
	
	Запрос.УстановитьПараметр("Дата", НачалоДня(Дата));
	Запрос.УстановитьПараметр("Портфель", Портфель);
	Запрос.УстановитьПараметр("Счет47010", ПланыСчетов.ЕПС.СчетаПоСтроке("47010"));
	Запрос.УстановитьПараметр("ПоВыбраннымАктивам", ПоВыбраннымАктивам);
	Запрос.УстановитьПараметр("СписокВыбранныхАктивов", ВыбранныеАктивы.ВыгрузитьКолонку("Актив"));
	Запрос.УстановитьПараметр("МассивСубконто", МассивСубконто);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции