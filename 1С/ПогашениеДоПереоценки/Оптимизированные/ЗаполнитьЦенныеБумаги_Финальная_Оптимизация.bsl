Процедура ЗаполнитьЦенныеБумаги(ИмяТабличнойЧасти, СписокАктивовПоОстаткам, ОблигацииДефолтныхЭмитентов, ТаблицаАмортизации)
	
	ЭтотОбъект[ИмяТабличнойЧасти].Очистить();
	ОфертыИсключения = РегистрыСведений.ОфертыИсключения.ОфертыПортфеля(Портфель);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"// ИЗМЕНЕНИЕ: Удален блок получения ВТ_ВидыОбращаемости из начала запроса
		|// Виды обращаемости теперь получаются только для активов, попавших в финальный результат
		|// ОПТИМИЗАЦИЯ: Это позволяет избежать запроса видов для всех активов из СписокАктивовПоОстаткам
		|
		|ВЫБРАТЬ
		|	ТаблицаАмортизации.Облигация КАК Облигация,
		|	ТаблицаАмортизации.НоминальнаяСтоимость КАК НоминальнаяСтоимость,
		|	ТаблицаАмортизации.ПроцентНоминала КАК ПроцентНоминала,
		|	ТаблицаАмортизации.СуммаПогашения КАК СуммаПогашения,
		|	ТаблицаАмортизации.ОстатокНоминала КАК ОстатокНоминала,
		|	ТаблицаАмортизации.КоэффициентАмортизации КАК КоэффициентАмортизации,
		|	ТаблицаАмортизации.Эмитент КАК Эмитент,
		|	ТаблицаАмортизации.Актив КАК Актив
		|ПОМЕСТИТЬ ВТ_АмортизацияТекущегоДня
		|ИЗ
		|	&ТаблицаАмортизации КАК ТаблицаАмортизации
		|ГДЕ
		|	ТаблицаАмортизации.ОстатокНоминала = 0
		|
		|// ИНДЕКС: Актив - используется для соединения с ОбъектыАктивов в третьем подзапросе UNION
		|ИНДЕКСИРОВАТЬ ПО
		|	Актив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// ИЗМЕНЕНИЕ: Убрано поле ВидОбращаемости из запроса ОбъектыАктивов
		|// Теперь получаем только Объект и Актив без соединения с ВТ_ВидыОбращаемости
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Активы.Объект КАК Объект,
		|	Активы.Ссылка КАК Актив
		|ПОМЕСТИТЬ ОбъектыАктивов
		|ИЗ
		|	Справочник.Активы КАК Активы
		|ГДЕ
		|	Активы.Ссылка В(&СписокАктивов)
		|	И НЕ Активы.Объект В (&СписокОблигацийДефолтныхЭмитентов)
		|
		|// ИНДЕКС: Объект - используется для соединения с Справочник.Облигации (2 места в UNION)
		|//         Актив - используется для соединения с ВТ_АмортизацияТекущегоДня, ВТ_ДатыОфертОблигаций, Справочник.Активы
		|// ОПТИМИЗАЦИЯ 8.3.25+: Используем НАБОРАМ для создания двух отдельных индексов вместо составного
		|ИНДЕКСИРОВАТЬ ПО НАБОРАМ (
		|	(Объект),
		|	(Актив)
		|);
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыОфертОблигаций.Актив КАК Актив,
		|	ДатыОфертОблигаций.Актив.Объект КАК АктивОбъект,
		|	ДатыОфертОблигаций.Регистратор.ВидОферты КАК ВидОферты,
		|	ДатыОфертОблигаций.Регистратор.ЦенаОферты КАК ЦенаОферты
		|ПОМЕСТИТЬ ВТ_ОбязательнаяОферта
		|ИЗ
		|	РегистрСведений.ДатыОфертОблигаций КАК ДатыОфертОблигаций
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ДатыОфертОблигаций.Период, ДЕНЬ) = &НачалоДня
		|	И ДатыОфертОблигаций.Регистратор.ВидОферты В(&ВидыОфертПогашения)
		|	И ДатыОфертОблигаций.ВидДаты = ЗНАЧЕНИЕ(Перечисление.ВидыДатОферты.ДатаОферты)
		|	И ДатыОфертОблигаций.Актив В(&СписокАктивов)
		|	И НЕ ДатыОфертОблигаций.Регистратор В (&ОфертыИсключения)
		|
		|// ИНДЕКС: Актив - используется для соединения с ВТ_АмортизацияТекущегоДня в третьем подзапросе UNION
		|//         АктивОбъект - используется для соединения с ОблигацииАмортизационныеВыплаты в четвертом подзапросе UNION
		|ИНДЕКСИРОВАТЬ ПО
		|	Актив,
		|	АктивОбъект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыОфертОблигаций.Период КАК Период,
		|	ДатыОфертОблигаций.Регистратор КАК Регистратор,
		|	ДатыОфертОблигаций.Актив КАК Актив
		|ПОМЕСТИТЬ ВТ_ДатыОфертОблигаций
		|ИЗ
		|	РегистрСведений.ДатыОфертОблигаций КАК ДатыОфертОблигаций
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ДатыОфертОблигаций.Период, ДЕНЬ) = &ДатаПогашения
		|	И ДатыОфертОблигаций.ВидДаты = ЗНАЧЕНИЕ(Перечисление.ВидыДатОферты.ДатаОферты)
		|	И ДатыОфертОблигаций.Регистратор.ВидОферты В(&ВидыОфертПогашения)
		|	И ВЫБОР
		|			КОГДА ДатыОфертОблигаций.Регистратор.ОфертаОтменена
		|				ТОГДА ДатыОфертОблигаций.Регистратор.ДатаОтменыОферты > &КонецДня
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И НЕ ДатыОфертОблигаций.Регистратор В (&ОфертыИсключения)
		|
		|// ИНДЕКС: Актив - используется для соединения с ОбъектыАктивов во втором подзапросе UNION
		|ИНДЕКСИРОВАТЬ ПО
		|	Актив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// ИЗМЕНЕНИЕ: Убрано поле ВидОбращаемости из всех подзапросов UNION
		|// Результат помещается во временную таблицу ВТ_РезультатБезВидовОбращаемости
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Актив КАК Актив,
		|	ВложенныйЗапрос.Облигация КАК Облигация,
		|	ВложенныйЗапрос.НоминальнаяСтоимость КАК НоминальнаяСтоимость,
		|	ВложенныйЗапрос.ИндексируемыйНоминал КАК ИндексируемыйНоминал,
		|	ВложенныйЗапрос.Амортизационная КАК Амортизационная,
		|	ВложенныйЗапрос.Оферта_MCAL_BN КАК Оферта_MCAL_BN,
		|	ВложенныйЗапрос.ПогашениеПоОферте КАК ПогашениеПоОферте,
		|	ВложенныйЗапрос.Приоритет КАК Приоритет,
		|	ВложенныйЗапрос.ЦенаОферты КАК ЦенаОферты
		|ПОМЕСТИТЬ ВТ_РезультатБезВидовОбращаемости
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОбъектыАктивов.Актив КАК Актив,
		|		ОбъектыАктивов.Объект КАК Облигация,
		|		Облигации.НоминальнаяСтоимость КАК НоминальнаяСтоимость,
		|		Облигации.ИндексируемыйНоминал КАК ИндексируемыйНоминал,
		|		Облигации.Амортизационная КАК Амортизационная,
		|		ЛОЖЬ КАК Оферта_MCAL_BN,
		|		ЛОЖЬ КАК ПогашениеПоОферте,
		|		2 КАК Приоритет,
		|		0 КАК ЦенаОферты
		|	ИЗ
		|		ОбъектыАктивов КАК ОбъектыАктивов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Облигации КАК Облигации
		|			ПО ОбъектыАктивов.Объект = Облигации.Ссылка
		|	ГДЕ
		|		Облигации.ДатаПогашения = &ДатаПогашения
		|		И НЕ Облигации.Амортизационная
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОбъектыАктивов.Актив,
		|		ОбъектыАктивов.Объект,
		|		ВЫБОР
		|			КОГДА ДатыОфертОблигаций.Регистратор.ВидОферты = ЗНАЧЕНИЕ(Перечисление.ВидыОферты.MCAL_BN)
		|				ТОГДА ДатыОфертОблигаций.Регистратор.ЦенаОферты
		|			ИНАЧЕ ВЫБОР
		|					КОГДА ДатыОфертОблигаций.Регистратор.ЦенаОферты = 0
		|						ТОГДА 100
		|					ИНАЧЕ ДатыОфертОблигаций.Регистратор.ЦенаОферты
		|				КОНЕЦ
		|		КОНЕЦ,
		|		Облигации.ИндексируемыйНоминал,
		|		Облигации.Амортизационная,
		|		ДатыОфертОблигаций.Регистратор.ВидОферты = ЗНАЧЕНИЕ(Перечисление.ВидыОферты.MCAL_BN),
		|		ИСТИНА,
		|		1,
		|		0
		|	ИЗ
		|		ОбъектыАктивов КАК ОбъектыАктивов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Облигации КАК Облигации
		|			ПО ОбъектыАктивов.Объект = Облигации.Ссылка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДатыОфертОблигаций КАК ДатыОфертОблигаций
		|			ПО ОбъектыАктивов.Актив = ДатыОфертОблигаций.Актив
		|	ГДЕ
		|		НЕ Облигации.Амортизационная
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВТ_АмортизацияТекущегоДня.Актив,
		|		ВТ_АмортизацияТекущегоДня.Облигация,
		|		ВТ_АмортизацияТекущегоДня.СуммаПогашения,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		1,
		|		0
		|	ИЗ
		|		ВТ_АмортизацияТекущегоДня КАК ВТ_АмортизацияТекущегоДня
		|			ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыАктивов КАК ОбъектыАктивов
		|			ПО ВТ_АмортизацияТекущегоДня.Актив = ОбъектыАктивов.Актив
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбязательнаяОферта КАК ВТ_ОбязательнаяОферта
		|			ПО ВТ_АмортизацияТекущегоДня.Актив = ВТ_ОбязательнаяОферта.Актив
		|	ГДЕ
		|		НЕ ВТ_АмортизацияТекущегоДня.Облигация В (&СписокОблигацийДефолтныхЭмитентов)
		|		И ВТ_ОбязательнаяОферта.Актив ЕСТЬ NULL
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Активы.Ссылка,
		|		ОблигацииАмортизационныеВыплаты.Ссылка,
		|		СУММА(ОблигацииАмортизационныеВыплаты.СуммаПогашения),
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		1,
		|		ВЫБОР
		|			КОГДА ВТ_ОбязательнаяОферта.ВидОферты = ЗНАЧЕНИЕ(Перечисление.ВидыОферты.MCAL_BN)
		|				ТОГДА ВТ_ОбязательнаяОферта.ЦенаОферты
		|			ИНАЧЕ ВЫБОР
		|					КОГДА ВТ_ОбязательнаяОферта.ЦенаОферты = 0
		|						ТОГДА 100
		|					ИНАЧЕ ВТ_ОбязательнаяОферта.ЦенаОферты
		|				КОНЕЦ
		|		КОНЕЦ
		|	ИЗ
		|		Справочник.Облигации.АмортизационныеВыплаты КАК ОблигацииАмортизационныеВыплаты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбязательнаяОферта КАК ВТ_ОбязательнаяОферта
		|			ПО ОблигацииАмортизационныеВыплаты.Ссылка = ВТ_ОбязательнаяОферта.АктивОбъект
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Активы КАК Активы
		|				ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыАктивов КАК ОбъектыАктивов
		|				ПО Активы.Ссылка = ОбъектыАктивов.Актив
		|			ПО ОблигацииАмортизационныеВыплаты.Ссылка = Активы.Объект
		|	ГДЕ
		|		НЕ ОблигацииАмортизационныеВыплаты.Ссылка В (&СписокОблигацийДефолтныхЭмитентов)
		|		И ОблигацииАмортизационныеВыплаты.ДатаПогашения >= &НачалоДня
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Активы.Ссылка,
		|		ОблигацииАмортизационныеВыплаты.Ссылка,
		|		ВЫБОР
		|			КОГДА ВТ_ОбязательнаяОферта.ВидОферты = ЗНАЧЕНИЕ(Перечисление.ВидыОферты.MCAL_BN)
		|				ТОГДА ВТ_ОбязательнаяОферта.ЦенаОферты
		|			ИНАЧЕ ВЫБОР
		|					КОГДА ВТ_ОбязательнаяОферта.ЦенаОферты = 0
		|						ТОГДА 100
		|					ИНАЧЕ ВТ_ОбязательнаяОферта.ЦенаОферты
		|				КОНЕЦ
		|		КОНЕЦ) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// НОВЫЙ БЛОК: Получаем виды обращаемости ТОЛЬКО для активов, попавших в результат
		|// Обращаемся напрямую к ВТ_РезультатБезВидовОбращаемости через подзапрос
		|// ОПТИМИЗАЦИЯ: Убрана промежуточная таблица ВТ_СписокАктивовДляВидовОбращаемости
		|// Преимущество: Запрос к РегистрСведений только для уникальных активов из финального результата
		|ВЫБРАТЬ
		|	ВидОбращаемостиЦБНаОРЦБСрезПоследних.Актив КАК Актив,
		|	ВидОбращаемостиЦБНаОРЦБСрезПоследних.ВидОбращаемости КАК ВидОбращаемости
		|ПОМЕСТИТЬ ВТ_ВидыОбращаемости
		|ИЗ
		|	РегистрСведений.ВидОбращаемостиЦБНаОРЦБ.СрезПоследних(&КонецДня, Актив В (ВЫБРАТЬ РАЗЛИЧНЫЕ Актив ИЗ ВТ_РезультатБезВидовОбращаемости)) КАК ВидОбращаемостиЦБНаОРЦБСрезПоследних
		|
		|// ИНДЕКС: Актив - используется для соединения с ВТ_РезультатБезВидовОбращаемости в финальном запросе
		|ИНДЕКСИРОВАТЬ ПО
		|	Актив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// ФИНАЛЬНЫЙ ЗАПРОС: Присоединяем виды обращаемости к результату
		|// Используем ЛЕВОЕ СОЕДИНЕНИЕ, так как для некоторых активов виды обращаемости могут отсутствовать
		|ВЫБРАТЬ
		|	ВТ_РезультатБезВидовОбращаемости.Актив КАК Актив,
		|	ВТ_РезультатБезВидовОбращаемости.Облигация КАК Облигация,
		|	ВТ_РезультатБезВидовОбращаемости.НоминальнаяСтоимость КАК НоминальнаяСтоимость,
		|	ВТ_РезультатБезВидовОбращаемости.ИндексируемыйНоминал КАК ИндексируемыйНоминал,
		|	ВТ_РезультатБезВидовОбращаемости.Амортизационная КАК Амортизационная,
		|	ВТ_РезультатБезВидовОбращаемости.Оферта_MCAL_BN КАК Оферта_MCAL_BN,
		|	ВТ_ВидыОбращаемости.ВидОбращаемости КАК ВидОбращаемости,
		|	ВТ_РезультатБезВидовОбращаемости.ПогашениеПоОферте КАК ПогашениеПоОферте,
		|	ВТ_РезультатБезВидовОбращаемости.Приоритет КАК Приоритет,
		|	ВТ_РезультатБезВидовОбращаемости.ЦенаОферты КАК ЦенаОферты
		|ИЗ
		|	ВТ_РезультатБезВидовОбращаемости КАК ВТ_РезультатБезВидовОбращаемости
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВидыОбращаемости КАК ВТ_ВидыОбращаемости
		|		ПО ВТ_РезультатБезВидовОбращаемости.Актив = ВТ_ВидыОбращаемости.Актив
		|
		|УПОРЯДОЧИТЬ ПО
		|	Актив,
		|	Приоритет";
	
	Запрос.УстановитьПараметр("ТаблицаАмортизации", ТаблицаАмортизации);
	Запрос.УстановитьПараметр("СписокАктивов", ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокАктивовПоОстаткам));
	Запрос.УстановитьПараметр("СписокОблигацийДефолтныхЭмитентов", ОблигацииДефолтныхЭмитентов);
	Запрос.УстановитьПараметр("ДатаПогашения", НачалоДня(Дата));
	Запрос.УстановитьПараметр("Портфель", Портфель);
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(Дата));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(Дата));
	Запрос.УстановитьПараметр("ВидыОфертПогашения", РегистрыСведений.ДатыОфертОблигаций.ВидыОфертПогашениеАктива(Портфель, КонецДня(Дата)));
	Запрос.УстановитьПараметр("ОфертыИсключения", ОфертыИсключения);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ОбщегоНазначенияДУ.ДобавитьВЛогОтладки(Запрос, "" + Ссылка + "Документ.ПогашениеОблигаций.МодульОбъекта: ЗаполнитьЦенныеБумаги");
	
	ПредыдущийАктив = Неопределено;
	Для Каждого ТекСтрока Из Результат Цикл
		
		Если ТекСтрока.Актив = ПредыдущийАктив Тогда
			Продолжить;
		КонецЕсли;
		
		ПредыдущийАктив = ТекСтрока.Актив;
		Если ТекСтрока.ПогашениеПоОферте И НЕ ТекСтрока.Амортизационная Тогда
			НоминальнаяСтоимость = Справочники.Облигации.ПолучитьНоминальнуюСтоимость(ТекСтрока.Облигация, НачалоДня(Дата));
			ЦенаОферты = ТекСтрока.НоминальнаяСтоимость;
			ТекСтрока.НоминальнаяСтоимость = Окр(ЦенаОферты * НоминальнаяСтоимость / 100, 6);
		ИначеЕсли ТекСтрока.ПогашениеПоОферте И ТекСтрока.Амортизационная Тогда
			ТекСтрока.НоминальнаяСтоимость = Окр(ТекСтрока.ЦенаОферты * ТекСтрока.НоминальнаяСтоимость / 100, 2);
		ИначеЕсли ТекСтрока.ИндексируемыйНоминал Тогда
			НоминальнаяСтоимость = Справочники.Облигации.ПолучитьНоминальнуюСтоимость(ТекСтрока.Облигация, НачалоДня(Дата));
			ТекСтрока.НоминальнаяСтоимость = НоминальнаяСтоимость;
		КонецЕсли;
		
		НоваяСтрока = ЭтотОбъект[ИмяТабличнойЧасти].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		
	КонецЦикла;
	
КонецПроцедуры