Функция ОстаткиОблигации(Активы, Счета, ФиксироватьДисконтПремию = Ложь)

	ЧастиАмортизацияДисконтаПремии = Новый Массив;
	ЧастиАмортизацияДисконтаПремии.Добавить(Перечисления.ЧастиЦенныхБумаг.АмортизацияДисконта);
	ЧастиАмортизацияДисконтаПремии.Добавить(Перечисления.ЧастиЦенныхБумаг.Премия);

	ЧастиОблигаций = Новый Массив;
	ЧастиОблигаций.Добавить(Перечисления.ЧастиЦенныхБумаг.ЦеннаяБумага);
	ЧастиОблигаций.Добавить(Перечисления.ЧастиЦенныхБумаг.ИндексацияНоминала);

	ПараметрыПортфеля = ПараметрыПортфеля();
	Если Не ПараметрыПортфеля.НеПереоцениватьЧастиЦБ_Комиссий Тогда
		ЧастиОблигаций.Добавить(Перечисления.ЧастиЦенныхБумаг.КомиссияБиржи);
		ЧастиОблигаций.Добавить(Перечисления.ЧастиЦенныхБумаг.КомиссияБрокера);
		ЧастиОблигаций.Добавить(Перечисления.ЧастиЦенныхБумаг.ИнаяКомиссия);
	КонецЕсли;

	ЧастиКомиссий = Новый Массив;
	ЧастиКомиссий.Добавить(Перечисления.ЧастиЦенныхБумаг.КомиссияБиржи);
	ЧастиКомиссий.Добавить(Перечисления.ЧастиЦенныхБумаг.КомиссияБрокера);
	ЧастиКомиссий.Добавить(Перечисления.ЧастиЦенныхБумаг.ИнаяКомиссия);

	ДатаЗаполнения = ДатаЗаполнения();

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса_ОстаткиОблигации();
	Запрос.УстановитьПараметр("Дата", ДатаЗаполнения);
	Запрос.УстановитьПараметр("Активы", Активы);
	Запрос.УстановитьПараметр("Портфель", Портфель);
	Запрос.УстановитьПараметр("Счета", Счета);
	Запрос.УстановитьПараметр("ЧастиОблигации", ЧастиОблигаций);
	Запрос.УстановитьПараметр("ЧастиКомиссий", ЧастиКомиссий);
	Запрос.УстановитьПараметр("СчетаУчетаИндексацииНоминала", УчетЕПСПовтИсп.СчетаУчетаИндексацииНоминала());
	Запрос.УстановитьПараметр("ФиксироватьДисконтПремию", ФиксироватьДисконтПремию);
	Запрос.УстановитьПараметр("ЧастиАмортизацияДисконтаПремии", ЧастиАмортизацияДисконтаПремии);

	ИмяСекцииЛога = ИмяСекцииЛога("ОстаткиОблигации");
	ЛогОтладки.Добавить(Запрос, ИмяСекцииЛога);

	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("Актив");

	Возврат Результат;

КонецФункции

Функция ТекстЗапроса_ОстаткиОблигации()
	Возврат "ВЫБРАТЬ
		|	ЗафиксированнаяПремияДисконтСрезПоследних.Портфель КАК Портфель,
		|	ЗафиксированнаяПремияДисконтСрезПоследних.Актив КАК Актив,
		|	ЗафиксированнаяПремияДисконтСрезПоследних.Партия КАК Партия,
		|	ЗафиксированнаяПремияДисконтСрезПоследних.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_ЗафиксированнаяПремияДисконт
		|ИЗ
		|	РегистрСведений.ЗафиксированнаяПремияДисконт.СрезПоследних(
		|			&Дата,
		|			&ФиксироватьДисконтПремию
		|				И Портфель = &Портфель
		|				И Актив В (&Активы)) КАК ЗафиксированнаяПремияДисконтСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕПСОстатки.Счет КАК СчетУчетаАктива,
		|	ЕПСОстатки.Субконто1 КАК Актив,
		|	ЕПСОстатки.Субконто3 КАК Партия,
		|	ЕПСОстатки.Валюта КАК Валюта,
		|	ЕПСОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ЕПСОстатки.ВалютнаяСуммаОстаток КАК ВалютнаяСуммаОстаток,
		|	ЕПСОстатки.СуммаОстаток КАК СуммаОстаток,
		|	0 КАК ВалютнаяСуммаОстатокКомиссий,
		|	0 КАК СуммаОстатокКомиссий
		|ПОМЕСТИТЬ ВТ_ОстаткиНесвернутые
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(
		|			&Дата,
		|			Счет В (&Счета),
		|			,
		|			Портфель = &Портфель
		|				И Субконто1 В (&Активы)
		|				И Субконто2 В (&ЧастиОблигации)) КАК ЕПСОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЕПСОстатки.Счет,
		|	ЕПСОстатки.Субконто1,
		|	ЕПСОстатки.Субконто3,
		|	ЕПСОстатки.Валюта,
		|	0,
		|	0,
		|	0,
		|	ЕПСОстатки.ВалютнаяСуммаОстаток,
		|	ЕПСОстатки.СуммаОстаток
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(
		|			&Дата,
		|			Счет В (&Счета),
		|			,
		|			Портфель = &Портфель
		|				И Субконто1 В (&Активы)
		|				И Субконто2 В (&ЧастиКомиссий)) КАК ЕПСОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕПСОстатки.СчетУчетаАктива КАК СчетУчетаАктива,
		|	ЕПСОстатки.Актив КАК Актив,
		|	ЕПСОстатки.Партия КАК Партия,
		|	ЕПСОстатки.Валюта КАК Валюта,
		|	СУММА(ЕПСОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	СУММА(ЕПСОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток,
		|	СУММА(ЕПСОстатки.СуммаОстаток) КАК СуммаОстаток,
		|	СУММА(ЕПСОстатки.ВалютнаяСуммаОстатокКомиссий) КАК ВалютнаяСуммаОстатокКомиссий,
		|	СУММА(ЕПСОстатки.СуммаОстатокКомиссий) КАК СуммаОстатокКомиссий
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	ВТ_ОстаткиНесвернутые КАК ЕПСОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕПСОстатки.СчетУчетаАктива,
		|	ЕПСОстатки.Актив,
		|	ЕПСОстатки.Партия,
		|	ЕПСОстатки.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта,
		|	Актив,
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕПСОстатки.Валюта КАК Валюта,
		|	ЕПСОстатки.Субконто1 КАК Субконто1,
		|	ЕПСОстатки.Субконто3 КАК Субконто3,
		|	СУММА(ЕПСОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток,
		|	СУММА(ЕПСОстатки.СуммаОстаток) КАК СуммаОстаток
		|ПОМЕСТИТЬ ВТ_ОстаткиИндексацияНоминала
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(
		|			&Дата,
		|			Счет В (&СчетаУчетаИндексацииНоминала),
		|			,
		|			Портфель = &Портфель
		|				И Субконто1 В (&Активы)
		|				И Субконто2 = ЗНАЧЕНИЕ(Перечисление.ЧастиЦенныхБумаг.ИндексацияНоминала)) КАК ЕПСОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕПСОстатки.Валюта,
		|	ЕПСОстатки.Субконто1,
		|	ЕПСОстатки.Субконто3
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта,
		|	Субконто1,
		|	Субконто3
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕПСОстатки.Валюта КАК Валюта,
		|	ЕПСОстатки.Субконто1 КАК Субконто1,
		|	ЕПСОстатки.Субконто3 КАК Субконто3,
		|	СУММА(ЕПСОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток,
		|	СУММА(ЕПСОстатки.СуммаОстаток) КАК СуммаОстаток,
		|	ЕПСОстатки.Счет КАК Счет
		|ПОМЕСТИТЬ ВТ_ОстаткиПремияДисконт
		|ИЗ
		|	РегистрБухгалтерии.ЕПС.Остатки(
		|			&Дата,
		|			Счет В (&Счета),
		|			,
		|			Портфель = &Портфель
		|				И Субконто1 В (&Активы)
		|				И Субконто2 В (&ЧастиАмортизацияДисконтаПремии)) КАК ЕПСОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕПСОстатки.Валюта,
		|	ЕПСОстатки.Субконто1,
		|	ЕПСОстатки.Субконто3,
		|	ЕПСОстатки.Счет
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта,
		|	Субконто1,
		|	Субконто3
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Остатки.СчетУчетаАктива КАК СчетУчетаАктива,
		|	ВТ_Остатки.Актив КАК Актив,
		|	ВТ_Остатки.Партия КАК Партия,
		|	ВТ_Остатки.Валюта КАК Валюта,
		|	СУММА(ВТ_Остатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	СУММА(ВТ_Остатки.ВалютнаяСуммаОстаток + ЕСТЬNULL(ВТ_ОстаткиИндексацияНоминала.ВалютнаяСуммаОстаток, 0) + ВЫБОР
		|			КОГДА ВТ_ЗафиксированнаяПремияДисконт.Сумма ЕСТЬ NULL
		|				ТОГДА ЕСТЬNULL(ВТ_ОстаткиПремияДисконт.ВалютнаяСуммаОстаток, 0)
		|			ИНАЧЕ ВТ_ЗафиксированнаяПремияДисконт.Сумма * ВТ_Остатки.КоличествоОстаток
		|		КОНЕЦ) КАК ВалютнаяСуммаОстаток,
		|	СУММА(ВТ_Остатки.СуммаОстаток + ЕСТЬNULL(ВТ_ОстаткиИндексацияНоминала.СуммаОстаток, 0)) КАК СуммаОстаток,
		|	СУММА(ВТ_Остатки.ВалютнаяСуммаОстатокКомиссий) КАК ВалютнаяСуммаОстатокКомиссий,
		|	СУММА(ВТ_Остатки.СуммаОстатокКомиссий) КАК СуммаОстатокКомиссий
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиИндексацияНоминала КАК ВТ_ОстаткиИндексацияНоминала
		|		ПО (ВТ_ОстаткиИндексацияНоминала.Валюта = ВТ_Остатки.Валюта)
		|			И (ВТ_ОстаткиИндексацияНоминала.Субконто1 = ВТ_Остатки.Актив)
		|			И (ВТ_ОстаткиИндексацияНоминала.Субконто3 = ВТ_Остатки.Партия)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗафиксированнаяПремияДисконт КАК ВТ_ЗафиксированнаяПремияДисконт
		|		ПО ВТ_Остатки.Актив = ВТ_ЗафиксированнаяПремияДисконт.Актив
		|			И ВТ_Остатки.Партия = ВТ_ЗафиксированнаяПремияДисконт.Партия
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиПремияДисконт КАК ВТ_ОстаткиПремияДисконт
		|		ПО ВТ_Остатки.Валюта = ВТ_ОстаткиПремияДисконт.Валюта
		|			И ВТ_Остатки.Актив = ВТ_ОстаткиПремияДисконт.Субконто1
		|			И ВТ_Остатки.Партия = ВТ_ОстаткиПремияДисконт.Субконто3
		|			И ВТ_Остатки.СчетУчетаАктива = ВТ_ОстаткиПремияДисконт.Счет
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Остатки.СчетУчетаАктива,
		|	ВТ_Остатки.Актив,
		|	ВТ_Остатки.Партия,
		|	ВТ_Остатки.Валюта";
КонецФункции
