
Функция МенеджерКотировокПоСпискуЦБ(Дата, СписокЦенныхБумаг) Экспорт
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Период", 	Дата);
	Запрос.УстановитьПараметр("СписокЦБ", 	СписокЦенныхБумаг);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ ТаблицаКотировок
		|ИЗ
		|	РегистрСведений.КотировкиЦБНаБирже КАК КотировкиЦБНаБирже
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(КотировкиЦБНаБирже.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
		|	И КотировкиЦБНаБирже.Актив В(&СписокЦБ)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КотировкиЦБНаБирже.Актив,
		|	КотировкиЦБНаБирже.Биржа";
				   
	Результат = Запрос.Выполнить();	
	
	ЗапросДляЛога = Запрос;
	ЗапросДляЛога.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОбщегоНазначенияДУ.ДобавитьВЛогОтладки(ЗапросДляЛога, "Документ.УстановкаКотировок.МенеджерКотировокПоСпискуЦБ");
	
	Возврат МенеджерВТ;
	
КонецФункции

Функция КотировкиПоБалансовойСтоимостиБУ(Знач ПараметрыРасчета)
		
	Запрос = Новый Запрос;	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Активы.Ссылка КАК Актив,
		|	ЕСТЬNULL(Активы.Объект.ВалютаНоминальнойСтоимости, ЗНАЧЕНИЕ(Справочник.Валюты.RUB)) КАК ВалютаНоминальнойСтоимости,
		|	Активы.ВидАктива КАК ВидАктива,
		|	Активы.Объект КАК Объект
		|ПОМЕСТИТЬ ВТ_Активы
		|ИЗ
		|	Справочник.Активы КАК Активы
		|ГДЕ
		|	Активы.Ссылка В(&МассивАктивов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект,
		|	ВалютаНоминальнойСтоимости
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Субконто1 КАК Объект,
		|	СУММА(ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт) КАК СуммаОстаток,
		|	СУММА(ХозрасчетныйОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	ВТ_Активы.Актив КАК Актив,
		|	ВТ_Активы.ВалютаНоминальнойСтоимости КАК ВалютаНоминальнойСтоимости,
		|	ВТ_Активы.ВидАктива КАК ВидАктива
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ДатаК,
		|			Счет В ИЕРАРХИИ (&СчетаАктивов)
		|				И НЕ Счет В (&СчетаНКД),
		|			,
		|			Портфель = &Портфель) КАК ХозрасчетныйОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Активы КАК ВТ_Активы
		|		ПО ХозрасчетныйОстатки.Субконто1 = ВТ_Активы.Объект
		|			И ХозрасчетныйОстатки.Валюта = ВТ_Активы.ВалютаНоминальнойСтоимости
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстатки.Субконто1,
		|	ВТ_Активы.Актив,
		|	ВТ_Активы.ВалютаНоминальнойСтоимости,
		|	ВТ_Активы.ВидАктива";
	
	Запрос.УстановитьПараметр("Портфель", 		ПараметрыРасчета.Портфель);
	Запрос.УстановитьПараметр("ДатаК", 			ПараметрыРасчета.ДатаОпределения);
	Запрос.УстановитьПараметр("МассивАктивов", 	ПараметрыРасчета.МассивЦББезКотировок);
	Запрос.УстановитьПараметр("СчетаАктивов",	БухгалтерскийУчетПовтИсп.СчетаУчетаНеденежныхАктивов());
	Запрос.УстановитьПараметр("СчетаНКД",	    БухгалтерскийУчетПовтИсп.СписокСчетовУчетаКупонногоДохода()); //TM-2800
		
	Результат = Запрос.Выполнить();
	
	ОбщегоНазначенияДУ.ДобавитьВЛогОтладки(Запрос, "Перечисление.МетодыРасчетаКотировок.МодульМенеджера:КотировкиПоБалансовойСтоимостиБУ");
	
	Выборка = Результат.Выбрать();
	
	Возврат Выборка;

КонецФункции

Функция СписокЦБ_БУ(МассивВидовАктивов)
	
	Если Не Портфель.БУ Тогда
		Возврат Новый Массив;;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыАктивов", МассивВидовАктивов);
	
	ПС = ПланыСчетов.Хозрасчетный;
	
	СчетаЦБ = Новый Массив;
	СчетаЦБ.Добавить(ПС.АкцииТело);
	СчетаЦБ.Добавить(ПС.ОблигацииТело);
	СчетаЦБ.Добавить(ПС.ПаиТело);
	СчетаЦБ.Добавить(ПС.ИпотечныеСертификатыТело);
	СчетаЦБ.Добавить(ПС.ДепозитарныеРаспискиТело);
	СчетаЦБ.Добавить(ПС.СтоимостьСтруктурнойНоты);
	
	Счет66_04_1 = ПС.ПолученныеЗаймыПоОперациямРЕПОТело;
	
	Запрос.УстановитьПараметр("Портфель", 	Портфель);
	Запрос.УстановитьПараметр("ДатаН", 		НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаК", 		КонецДня(Дата));
	Запрос.УстановитьПараметр("СчетаЦБ",	СчетаЦБ);
	Запрос.УстановитьПараметр("Счет66_04_1",	Счет66_04_1);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК Субконто1
		|ПОМЕСТИТЬ ТаблицаВсеЦБ
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&ДатаН, &ДатаК, Период, Счет В (&СчетаЦБ), , Портфель = &Портфель, , ) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	ХозрасчетныйОбороты.КоличествоОборот <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Субконто1
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаК, Счет В (&СчетаЦБ), , Портфель = &Портфель) КАК ХозрасчетныйОстатки
		|ГДЕ
		|	ХозрасчетныйОстатки.КоличествоОстаток <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.Активы).Объект
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&ДатаН, &ДатаК, Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбеспеченияОбязательствПолученные), , Портфель = &Портфель, , ) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	ХозрасчетныйОбороты.КоличествоОборот <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.Активы).Объект
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаК, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбеспеченияОбязательствПолученные), , Портфель = &Портфель) КАК ХозрасчетныйОстатки
		|ГДЕ
		|	ХозрасчетныйОстатки.КоличествоОстаток <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто3 КАК Справочник.Активы).Объект
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&ДатаН, &ДатаК, Период, Счет = &Счет66_04_1, , Портфель = &Портфель, , ) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	ХозрасчетныйОбороты.КоличествоОборот <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто3 КАК Справочник.Активы).Объект
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаК, Счет = &Счет66_04_1, , Портфель = &Портфель) КАК ХозрасчетныйОстатки
		|ГДЕ
		|	ХозрасчетныйОстатки.КоличествоОстаток <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаВсеЦБ.Субконто1 КАК Субконто1
		|ПОМЕСТИТЬ ТаблицаЦБ
		|ИЗ
		|	ТаблицаВсеЦБ КАК ТаблицаВсеЦБ
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаВсеЦБ.Субконто1
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Субконто1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Активы.Ссылка КАК Актив,
		|	Активы.Объект,
		|	Активы.ВидАктива
		|ИЗ
		|	ТаблицаЦБ КАК ТаблицаЦБ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Активы КАК Активы
		|		ПО ТаблицаЦБ.Субконто1 = Активы.Объект
		|ГДЕ
		|	Активы.ВидАктива В ИЕРАРХИИ(&ВидыАктивов)";
	
	ОбщегоНазначенияДУ.ДобавитьВЛогОтладки(Запрос, "Документ.УстановкаКотировокЦБ.МодульОбъекта:СписокЦБ_БУ");
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Актив");
	
КонецФункции

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ПометкаУдаления ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегламентноеЗакрытиеПериода.ОчиститьВыбранныеАктивы(ЭтотОбъект);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И КонтролироватьУникальностьУстановкиССВПределахДня() Тогда
		Если ВПределахДняЕстьДокументУстановкаСС() и Не ПоВыбраннымАктивам Тогда
			ВызватьИсключение("Запрещено проводить несколько документов ""Установка справедливой стоимости"" в один день.");
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Котировки Цикл
		Если НЕ СтрокаТЧ.Актив.ВидАктива.ПринадлежитЭлементу(ПланыВидовХарактеристик.ВидыАктивов.Облигации) 
			ИЛИ ПоВыбраннымАктивам Тогда
			СтрокаТЧ.НКД = 0;
		КонецЕсли;
	КонецЦикла;
	
	Если ОбщегоНазначенияДУСобытия.ДокументВходитВРегламентныеОперации(ДокументОснование) Тогда
		Отказ = Справочники.РегламентныеПериоды.ПериодЗакрыт(Дата, Истина, РеглПериод, Портфель);
	КонецЕсли;
	
КонецПроцедуры
