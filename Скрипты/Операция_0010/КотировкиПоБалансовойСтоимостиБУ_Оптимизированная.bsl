Функция КотировкиПоБалансовойСтоимостиБУ(Знач ПараметрыРасчета)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		// 1. ВТ_Объекты - только Объекты из справочника Активы
		"ВЫБРАТЬ
		|	Активы.Объект КАК Объект
		|ПОМЕСТИТЬ ВТ_Объекты
		|ИЗ
		|	Справочник.Активы КАК Активы
		|ГДЕ
		|	Активы.Ссылка В (&МассивАктивов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// 0. ВТ_Счета - плоский список всех счетов из группы СчетаАктивов без НКД
		|ВЫБРАТЬ
		|	Счета.Ссылка КАК Счет
		|ПОМЕСТИТЬ ВТ_Счета
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Счета
		|ГДЕ
		|	Счета В ИЕРАРХИИ (&СчетаАктивов)
		|		И НЕ Счета В (&СчетаНКД)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// 2. ВТ_Остатки - остатки только по объектам из ВТ_Объекты, через условие Субконто1 В (ВТ)
		|ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Субконто1 КАК Объект,
		|	ХозрасчетныйОстатки.Валюта   КАК Валюта,
		|	ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт КАК СуммаОстаток,
		|	ХозрасчетныйОстатки.КоличествоОстаток     КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ДатаК,
		|			Счет В (ВЫБРАТЬ Счет КАК Счет ИЗ ВТ_Счета),
		|			,
		|			Портфель = &Портфель
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						ВТ_Объекты.Объект КАК Объект
		|					 ИЗ
		|						ВТ_Объекты КАК ВТ_Объекты)
		|	) КАК ХозрасчетныйОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект,
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// 3. Финальный запрос - фильтрация по валюте + обогащение полями из справочника
		|ВЫБРАТЬ
		|	ВТ_Остатки.Объект                     КАК Объект,
		|	СУММА(ВТ_Остатки.СуммаОстаток)               КАК СуммаОстаток,
		|	СУММА(ВТ_Остатки.КоличествоОстаток)          КАК КоличествоОстаток,
		|	Активы.Ссылка                         КАК Актив,
		|	ЕСТЬNULL(Активы.Объект.ВалютаНоминальнойСтоимости,
		|	         ЗНАЧЕНИЕ(Справочник.Валюты.RUB)) КАК ВалютаНоминальнойСтоимости,
		|	Активы.ВидАктива                      КАК ВидАктива
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Активы КАК Активы
		|		ПО ВТ_Остатки.Объект = Активы.Объект
		|			И ВТ_Остатки.Валюта = ЕСТЬNULL(Активы.Объект.ВалютаНоминальнойСтоимости,
		|			                              ЗНАЧЕНИЕ(Справочник.Валюты.RUB))
		|			И Активы.Ссылка В (&МассивАктивов)
		|
		|СГРУППИРОВАТЬ ПО
		|	Активы.Ссылка,
		|	Активы.ВидАктива,
		|	ЕСТЬNULL(Активы.Объект.ВалютаНоминальнойСтоимости,
		|	         ЗНАЧЕНИЕ(Справочник.Валюты.RUB)),
		|	ВТ_Остатки.Объект
		|";
	
	// Параметры
	Запрос.УстановитьПараметр("Портфель", ПараметрыРасчета.Портфель);
	Запрос.УстановитьПараметр("ДатаК", ПараметрыРасчета.ДатаОпределения);
	Запрос.УстановитьПараметр("МассивАктивов", ПараметрыРасчета.МассивЦББезКотировок);
	Запрос.УстановитьПараметр("СчетаАктивов", БухгалтерскийУчетПовтИсп.СчетаУчетаНеденежныхАктивов());
	Запрос.УстановитьПараметр("СчетаНКД", БухгалтерскийУчетПовтИсп.СписокСчетовУчетаКупонногоДохода());
	
	Результат = Запрос.Выполнить();
	
	ОбщегоНазначенияДУ.ДобавитьВЛогОтладки(
		Запрос,
		"Перечисление.МетодыРасчетаКотировок.МодульМенеджера:КотировкиПоБалансовойСтоимостиБУ");
	
	Возврат Результат.Выбрать();
	
КонецФункции